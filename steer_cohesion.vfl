// parameter interface
int max_pts = chi("max_points");
float rad = ch("radius");
float priority = ch("priority");
vector scale = chv("scale");
float force_scale = ch("force_scale");
float min_dist = ch("min_distance");
float max_dist = ch("max_distance");

// initial variables
vector steer = set(0, 0, 0);
int handle = pcopen(0, "P", @P, rad, max_pts);

// neighbourhood lookup loop
vector neighp;
int neighpt;

while(pciterate(handle)){
    pcimport(handle, "point.number", neighpt);
    if(neighpt == @ptnum)
        continue; 
    pcimport(handle, "P", neighp);
    steer += neighp;
}

steer /= pcnumfound(handle);
float dist_scale = fit(distance(steer, @P), min_dist, max_dist, 0, 1);
steer = (steer - @P) * priority * force_scale * scale * dist_scale;

// steerforce attribute creation
string steerforce = concat("steerforce", itoa(i@steercount));
string steerpriority = concat("steerpriority", itoa(i@steercount));
addpointattrib(0, steerforce, set(0, 0, 0));
setpointattrib(0, steerforce, @ptnum, steer);
addpointattrib(0, steerpriority, 0.0);
setpointattrib(0, steerpriority, @ptnum, priority);
i@steercount++;